;;; ebnf-mode.el --- Major mode (E)BNF grammars -*- lexical-binding: t -*-
;;
;;; Commentary:
;; Major mode for editing files containing various styles of (E)BNF grammars.
;; Currently supports the following styles:
;;
;;  - iso-ebnf :: `http://www.cl.cam.ac.uk/~mgk25/iso-ebnf.html'
;;  - ebnf     :: Style built into `ebnf2ps.el'
;;  - abnf     :: `http://www.ietf.org/rfc/rfc2234.txt'
;;  - ebnfx    :: `http://www.w3.org/TR/2004/REC-xml-20040204/#sec-notation'
;;
;;; Code:

(require 'smie)

(defgroup ebnf-mode nil
  "(E)BNF editing support for Emacs."
  :group 'languages
  :version "25.1")


(defcustom ebnf-mode-style 'iso-ebnf
  "The EBNF style to associate with the current buffer."
  :group 'ebnf-mode
  :type '(radio :tag "Style"
                (const iso-ebnf)
		(const ebnf)
                (const abnf)
                (const ebnfx))
  :safe #'symbolp)


;;;; Fontification.


(defvar ebnf-mode-lhs-regexp
  (rx (and (* space)
           (group-n 1 (in "A-Z" "a-z")
                    (+ (in "A-Z" "a-z" "0-9" "_" "-")))
           (* space) (or "=" "::=")))
  "Regular expression to match and fontify the (E)BNF left hand sides.")


(defvar ebnf-mode-font-lock-keywords
  `(((,ebnf-mode-lhs-regexp (1 font-lock-variable-name-face))))
  "List over `font-lock' specifiers.")


;;;; SMIE indentation setup.


(defcustom ebnf-mode-indent-offset 4
  "(E)BNF indentation width."
  :group 'ebnf-mode
  :type 'integer
  :safe #'integerp)


(defun ebnf-debug-lexer (fun)
  "Debug the lexer FUN."
  (lambda ()
    (let ((tok (funcall fun))
          (nam (symbol-name fun))
          (pos (point)))
      (princ (format "%s: %s at %d\n" nam tok pos))
      tok)))


(defun ebnf-mode-new-rule-p ()
  "Check if there is a new rule to be found by looking forward."
  (save-excursion
    (forward-comment (point-max))
    (looking-at-p ebnf-mode-lhs-regexp)))


;;;; `iso-ebnf' style rules.


(defconst ebnf-mode-iso-ebnf-syntax-table
  (let ((table (make-syntax-table)))
    (modify-syntax-entry ?'  "\""  table) ; String delimiter
    (modify-syntax-entry ?\" "\"" table)  ; String delimiter
    (modify-syntax-entry ?\( "()1" table)  ; Comment start
    (modify-syntax-entry ?*  ". 23" table) ; Comment start/end
    (modify-syntax-entry ?\) ")(4" table)  ; Comment end
    table))


(defvar ebnf-mode-smie-iso-ebnf-grammar
  (smie-prec2->grammar
   (smie-bnf->prec2
    '((id)
      (lhs (id))
      (rhs (rhs "|" rhs)
           (rhs "," rhs)
           (id))
      (rule (lhs "=" rhs))
      (rules (rules ";" rules) (rule)))
    '((assoc ";"))
    '((assoc "|") (assoc ","))))
  "Simplfied BNF grammar for `smie' indentation of the `iso-ebnf' style.")


(defun ebnf-mode-smie-iso-ebnf-rules (kind token)
  "Perform indentation of KIND on TOKEN using the `smie' engine."
  (pcase (cons kind token)
    (`(:elem . basic)            ebnf-mode-indent-offset)
    (`(:elem . args)             ebnf-mode-indent-offset)
    (`(:list-intro . "=")        0)
    (`(:before . ,(or ";" "," "|"))  (smie-rule-separator kind))
    (`(:after . "=") ebnf-mode-indent-offset)))


;;;; `ebnf' style rules.


(defconst ebnf-mode-ebnf-syntax-table
  (let ((table (make-syntax-table)))
    (modify-syntax-entry ?'  "\"" table)  ; String delimiter
    (modify-syntax-entry ?\" "\"" table)  ; String delimiter
    (modify-syntax-entry ?\; "<"  table)  ; Comment start
    (modify-syntax-entry ?\n ">"  table)  ; Comment end
    (modify-syntax-entry ??  "$"  table)  ; paired delimiter
    table))


(defvar ebnf-mode-smie-ebnf-grammar
  (smie-prec2->grammar
   (smie-bnf->prec2
    '((id)
      (lhs (id))
      (alts (rhs "||" rhs)
            (rhs))
      (rhs ("{" alts "}")
           (rhs "|" rhs)
           (id))
      (rule (lhs "=" rhs))
      (rules (rules "." rules) (rule)))
    '((assoc "."))
    '((assoc "||") (assoc "|"))))
  "Simplfied BNF grammar for `smie' indentation of the `ebnf' style.")


(defun ebnf-mode-smie-ebnf-rules (kind token)
  "Perform indentation of KIND on TOKEN using the `smie' engine."
  (pcase (cons kind token)
    (`(:elem . basic)            ebnf-mode-indent-offset)
    (`(:elem . args)             ebnf-mode-indent-offset)
    (`(:list-intro . "=")        0)
    (`(:before . ,(or "." "|" "||"))  (smie-rule-separator kind))
    (`(:after . "=") ebnf-mode-indent-offset)))


;;;; `abnf' style rules.


(defconst ebnf-mode-abnf-syntax-table
  (let ((table (make-syntax-table)))
    (modify-syntax-entry ?'  "\"" table)  ; String delimiter
    (modify-syntax-entry ?\" "\"" table)  ; String delimiter
    (modify-syntax-entry ?\; "<"  table)  ; Comment start
    (modify-syntax-entry ?\n ">"  table)  ; Comment end
    table))


(defvar ebnf-mode-smie-abnf-grammar
  (smie-prec2->grammar
   (smie-bnf->prec2
    '((id)
      (lhs (id))
      (rhs (rhs "/" rhs)
           (id))
      (rule (lhs "=/" rhs)
            (lhs "="  rhs))
      (rules (rules "NEWLINE-SEPARATOR" rules) ; Token generated by lexer.
             (rule)))
    '((assoc "NEWLINE-SEPARATOR"))
    '((assoc "/"))))
  "Simplfied BNF grammar for `smie' indentation of the `abnf' style.")


(defun ebnf-mode-smie-abnf-rules (kind token)
  "Perform indentation of KIND on TOKEN using the `smie' engine."
  (pcase (cons kind token)
    (`(:elem . basic)            ebnf-mode-indent-offset)
    (`(:elem . args)             0)
    (`(:list-intro . ,(or "=" "=/"))        0)
    (`(:before . ,(or "NEWLINE-SEPARATOR" "/"))  (smie-rule-separator kind))
    (`(:after . ,(or "=" "=/"))  ebnf-mode-indent-offset)))


(defun ebnf-mode-smie-abnf-forward-token ()
  "Search forwards for a token to be used by `smie' to indent `abnf'."
  (let ((pos (point)))
    (forward-comment (point-max))
    (cond
     ((and (< pos (point))              ; Only emit separator if we have moved.
           (or (ebnf-mode-new-rule-p)
               (eobp)))
      (forward-comment (- (point)))
      "NEWLINE-SEPARATOR")
     (t
      (buffer-substring-no-properties
       (point)
       (progn (skip-syntax-forward "w_'")
              (point)))))))


(defun ebnf-mode-smie-abnf-backward-token ()
  "Search backwards for a token to be used by `smie' to indent `abnf'."
  (let ((pos (point)))
    (forward-comment (- (point)))
    (cond
     ((and (> pos (point))
           (ebnf-mode-new-rule-p))
      "NEWLINE-SEPARATOR")
     (t
      (forward-comment (- (point)))
      (buffer-substring-no-properties
       (point)
       (progn (skip-syntax-backward "w_")
              (point)))))))


;;;; `ebnfx' style rules.


(defvar ebnf-mode-ebnfx-smie-tokens-regexp
  (regexp-opt '("+" "-" "*" "?" "::=" "|"))
  "Token keywords the `smie' lexer should look for in the `ebnfx' style.")


(defconst ebnf-mode-ebnfx-syntax-table
  (let ((table (make-syntax-table)))
    (modify-syntax-entry ?'  "\""  table)  ; String delimiter
    (modify-syntax-entry ?\" "\""  table)  ; String delimiter
    (modify-syntax-entry ?/  ". 124" table)
    (modify-syntax-entry ?*  ". 23b" table)
    (modify-syntax-entry ?\n ">"   table)
    table))


(defvar ebnf-mode-smie-ebnfx-grammar
  (smie-prec2->grammar
   (smie-bnf->prec2
    '((id)
      (lhs (id))
      (rhs (rhs "|" rhs)
           (id))
      (rule (lhs "::=" rhs))
      (rules (rules "NEWLINE-SEPARATOR" rules) ; Token generated by lexer.
             (rule)))
    '((assoc "NEWLINE-SEPARATOR"))
    '((assoc "|"))))
  "Simplfied BNF grammar for `smie' indentation of the `ebnfx' style.")


(defun ebnf-mode-smie-ebnfx-rules (kind token)
  "Perform indentation of KIND on TOKEN using the `smie' engine."
  (pcase (cons kind token)
    (`(:elem . basic)            ebnf-mode-indent-offset)
    (`(:elem . args)             0)
    (`(:list-intro . "::=")      0)
    (`(:before . ,(or "NEWLINE-SEPARATOR" "|"))  (smie-rule-separator kind))
    (`(:after . "::=")           ebnf-mode-indent-offset)))


(defun ebnf-mode-smie-ebnfx-forward-token ()
  "Search forwards for a token to be used by `smie'."
  (let ((pos (point)))
    (forward-comment (point-max))
    (cond
     ((and (< pos (point))              ; Only emit separator if we have moved.
           (or (ebnf-mode-new-rule-p)
               (eobp)))
      (forward-comment (- (point)))
      "NEWLINE-SEPARATOR")
     ((looking-at ebnf-mode-ebnfx-smie-tokens-regexp)
      (goto-char (match-end 0))
      (match-string-no-properties 0))
     (t
      (buffer-substring-no-properties
       (point)
       (progn (skip-syntax-forward "w_'")
              (point)))))))


(defun ebnf-mode-smie-ebnfx-backward-token ()
  "Search backwards for a token to be used by `smie'."
  (let ((pos (point)))
    (forward-comment (- (point)))
    (cond
     ((and (> pos (point))
           (ebnf-mode-new-rule-p))
      "NEWLINE-SEPARATOR")
     ((looking-back ebnf-mode-ebnfx-smie-tokens-regexp (- (point) 3) t)
      (goto-char (match-beginning 0))
      (match-string-no-properties 0))
     (t
      (forward-comment (- (point)))
      (buffer-substring-no-properties
       (point)
       (progn (skip-syntax-backward "w_")
              (point)))))))


;;;; Major mode definitions.


(defun ebnf-iso-ebnf-setup ()
  "Setup the `major-mode' for the `iso-ebnf' style."
  (setq-local comment-start "(* ")
  (setq-local comment-end   " *)")
  (setq-local comment-start-skip "\\((\\*\\)\\s-*")
  (setq-local comment-end-skip   "\\s-*\\(\\*)\\)")
  (set-syntax-table ebnf-mode-iso-ebnf-syntax-table)
  (smie-setup ebnf-mode-smie-iso-ebnf-grammar #'ebnf-mode-smie-iso-ebnf-rules))


(defun ebnf-ebnf-setup ()
  "Setup the `major-mode' for the `ebnf' style."
  (setq-local comment-start "; ")
  (setq-local comment-start-skip ";+\\s-*")
  (set-syntax-table ebnf-mode-ebnf-syntax-table)
  (smie-setup ebnf-mode-smie-ebnf-grammar #'ebnf-mode-smie-ebnf-rules))


(defun ebnf-abnf-setup ()
  "Setup the `major-mode' for the `abnf' style."
  (setq-local comment-start "; ")
  (setq-local comment-start-skip ";+\\s-*")
  (set-syntax-table ebnf-mode-abnf-syntax-table)
  (smie-setup ebnf-mode-smie-abnf-grammar #'ebnf-mode-smie-abnf-rules
              :forward-token  #'ebnf-mode-smie-abnf-forward-token
              :backward-token #'ebnf-mode-smie-abnf-backward-token))


(defun ebnf-ebnfx-setup ()
  "Setup the `major-mode' for the `ebnfx' style."
  (setq-local comment-start "/* ")
  (setq-local comment-end   " */")
  (setq-local comment-start-skip "\\(/\\*\\)\\s-*")
  (setq-local comment-end-skip   "\\s-*\\(\\*/\\)")
  (set-syntax-table ebnf-mode-ebnfx-syntax-table)
  (smie-setup ebnf-mode-smie-ebnfx-grammar #'ebnf-mode-smie-ebnfx-rules
              :forward-token  #'ebnf-mode-smie-ebnfx-forward-token
              :backward-token #'ebnf-mode-smie-ebnfx-backward-token))


(defun ebnf-mode-after-hack-local-variables ()
  "Set EBNF style specific settings after parsing file-local variables."
  (pcase (list ebnf-mode-style)
    (`(iso-ebnf)  (ebnf-iso-ebnf-setup))
    (`(ebnf)      (ebnf-ebnf-setup))
    (`(abnf)      (ebnf-abnf-setup))
    (`(ebnfx)     (ebnf-ebnfx-setup))
    (`(,_)        (ebnf-iso-ebnf-setup))))


;;;###autoload
(define-derived-mode ebnf-mode prog-mode "EBNF"
  "Major mode for various EBNF formats."
  :group 'ebnf-mode
  (setq-local indent-tabs-mode nil)
  (setq-local font-lock-defaults ebnf-mode-font-lock-keywords)
  (add-hook 'hack-local-variables-hook
            #'ebnf-mode-after-hack-local-variables nil t))


;;;###autoload
(add-to-list 'auto-mode-alist '("\\.ebnf\\'" . ebnf-mode))

(provide 'ebnf-mode)

;;; ebnf-mode.el ends here
