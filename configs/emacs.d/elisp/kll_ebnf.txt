* Character classes. *
whitespace      = { space | newline | tab } ;
string          = '"', { all_characters - '"' }, '"' ;
nat_number      = 1-9, { 0-9 } ;
pos_number      = 0 | nat_number ;
hex_number      = "0x", pos_number ;
integer         = 0 | [ "-" ], nat_number ;
number          = hex_string | integer ;
alphabetic_char = A-Z | a-z ;
all_characters  = A-Z | a-z | 0-9 | _ ;


* Generic KLL statements. *
kll            = { ( statement, ";" | comment ), whitespace } ;
comment        = "#", { all_characters | space } ;
statement      = variable_stmt |
                 define_stmt |
                 capability_stmt |
                 key_mapping_stmt |
                 property_mapping |
                 pixel_mapping |
                 animation_mapping ;


* Variable statements. *
kll_id          = alphabetic_char, { all_characters } ;
var_id          = string | kll_id;
variable        = ( var_id, [ "[", pos_number, "]", ] ) | string | number ;
variable_assign = variable, "=", variable;
array_assign    = var_id, "[", "]", "=", { variable, "," }, variable ;
variable_stmt   = array_assign | variable_assign;


* Define statements. *
define_stmt = kll_id, "=>", kll_id ;


* Capability statements. *
capability_arg  = ( kll_id, ":", nat_number ) | "" ;
capability_args = { capability_arg, "," }, capability_arg ;
capability_stmt = kll_id, "=>", kll_id, "(", capability_args, ")" ;


* Key mappings. *
* TODO: Handle timing and analog values inside ranges. *
* TODO: Handle separate results and triggers. *
* TODO: Properly handle scan codes. *
scan_code     = "S", (pos_number | hex_number) ;
key_prefix    = "S" | "U" | "I" | "CON" | "SYS" ;
key           = pos_number | hex_number | string ;
key_delimiter = ":" | "::" | ":+" | ":-" | "i:" | "i::" | "i:+" | "i:-" ;
press_state   = "P" | "UP" | "UR" | "R" | "H" | "O" ;
key_state     = "(", pos_number | press_state, ")" ;
range         = key, "-", key ;
kll_range     = "[", { ( range | key ), [ key_state ], "," }, ( range | key ), [ key_state ], "]" ;
key_mapping   = key_prefix, ( key | kll_range ), [ "(" key_state ")" ];
key_mappings  = { key_mapping, [ ("," | "+") ] }, key_mapping ;

time/state    =
trigger_pre   = "U" | "I" | "CON" | "SYS" ;
trigger_code  = scan_code | ( trigger_pre, key ) ;
trigger       = trigger_code, "(", time/state, ")" ;
trigger_map   = trigger | trigger_range;
trigger_maps  = { trigger_map, ("," | "+") }, trigger_map;
key_trigger   = trigger_maps ;
key_result    = key_mappings ;
key_mapping_stmt = key_trigger, key_delimiter, key_result ;


* Scan-code property mapping. *
property         = kll_id, ":", integer ;
properties       = { property, "," }, property ;
property_mapping = "S", key, "<=", properties ;


* Pixel mappings. *
channel       = pos_number, ":", nat_number ;
channels      = { channel, "," }, channel ;
pixel_mapping = "P", "[", pos_number, "]", "(", channels, ")", ":", ( ("S", key) | "None" ) ;


* Animation mappings. *
idx_exp    = "i", [ "+", "-" ], pos_number, [ "%" ] ;
comp       = pos_number | idx_exp ;
exp        = pos_number | ( ("r" | "c"), ":" comp ) ;
address    = exp, [ ",", exp ] ;
op         = "" | "+" | "-" | "+:" | "-:" | "<<" | ">>" ;
values     = op, pos_number, [ op, pos_number, [ op, pos_number ] ] ;
pixel      = ( "P" | "PL" ), "[", address, "]", "(", values, ")" ;
kll_pixel  = pixel | ("U", key) | ("S", key) ;
kll_pixels = { kll_pixel, "," }, kll_pixel ;

interpolation     = "interp", ":", ( "on" | "off" | "kll" ) ;
modifier          = "loop" [ ":", pos_number ] | "div", ":", pos_number | "start" | "stop" | "frame", ":", pos_number | interpolation ;
modifiers         = { modifier, "," }, modifier ;
animation_frame   = "[", kll_id, "]", "<=", kll_pixels ;
animation_mod     = "[", kll_id, "]", "<=", modifiers ;
animation_mapping = "A", ( animation_mod | animation_frame ) ;
